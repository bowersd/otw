set encode-weights ON
!define cnjNasVowel                  [[..] -> h ||  e n _ .#.] ; !redundant with nasalEmergence
define finPostNasVoi                [p -> [b::2], t -> [d::2], k -> [g::2], s -> [z::2], s h -> [z h::2], c h -> [j::2]   || [m|n]  _ .#. ]; 
define finDevoi                     [b -> [p::2], d -> [t::2], g -> [k::2], z -> [s::2], z h -> [s h::2], j -> [c h::2]   ||  _ .#. ]; 
![b -> p || m _ .#. ,,
!                                    d -> t, g -> k, z -> s, z h -> s h, j -> c h || n _ .#. ]  ; 
define finLowering                  [o -> a, i -> e || \[a|i|o|e] _  (p|t|k|c h|
                                                                        [s (h)](p|t|k)|
                                                                        [m (b)]|[n (d|g|j|z (h)|h)]|
                                                                        w|y|%'|h) .#. ] ;
!!!!
define nasalEmergence               [h -> [[0]::2] || n _ .#.] ; 
define indDeletion                  [d -> [[0]::2] || i n _ .#.] ;
define nDeletion                    [n -> [[0]::2] ||  [a|i|o|e] _ .#.] ;
define vvnTovvnh                    [[..] -> [h::2] || [i i|a a|o o|e] n _ .#.] ; 

!!!!
define postYRaising                 [a (->) [i::1] || y _ \[a]] ; 
define glideBacknessAssimilation    [w (->) [y::2] || [a|e|i] _ ,,
                                    y (->)  [w::2] || o _ ] ; 
define codaWDeletionMAYBEONSET      [w (->) [0::2] || _ [b|d|g|p|t|k|z|s|j|c|m|n],,
                                     w (->) [0::3] || _ [a|i|o|e]
                                    ];
!define hw                           [h (->) [[w|%'|0]::2] || \[c|s|n|z] _ ];

!!!! Voice in clusters
define stridentDevoicing            [z h (->) [s h::2], z (->) [s::2] || _ [p|t|k|b|d|g]] ; 
define postStridentDevoicing        [b (->) [p::2], d (->) [t::2], g (->) [k::2] || [s (h)] _ ] ; 

!!!! Cluster adjustments
define homorgObsMerge [
                    b b (->) [p::2], b p (->) [p::2], p b (->) [p::2], p p (->) [p::2],
                    d d (->) [t::2], d t (->) [t::2], t d (->) [t::2], t t (->) [t::2],
                    g g (->) [k::2], g k (->) [k::2], k g (->) [k::2], k k (->) [k::2],
                    z z (->) [s::2], z s (->) [s::2], s z (->) [s::2], s s (->) [s::2],
                    z h z h (->) [s h::2], z h s h (->) [s h::2], s h z h (->) [s::2], sh sh (->) [s h::2],
                    j j (->) [c h::2], j c h (->) [c h::2], c h j (->) [c h::2], c h c h (->) [c h::2]
                    ]; 
define lenisNasAssim [ b (->) [m::2], g (->) [n g::2], d (->) [n::2] ||  _ [m|n] ,, !from Rhodes 1985b p 468
                       b n (->) [m::2] || _ d ] ; !If I had a dime for every bndam -> mdam...
define lenisStopDeletion [[d|b|g] (->) [0::2] ||  _ [[b|d|g|p|t|k|m (b)|n (d|[z (h)]|j)|[[z (h)]]|[[s] (h) (p|t|k)]j|c h] (w|y)] [a|i|o|e]] ; 
define lenisFortition [ b (->) [p::2], g (->) [k::2], d (->) [t::2], j (->) [c h::2], z h (->) [s h::2], z (->) [s::2] ||  _ [p|t|k|s|c h] ] ; !from Rhodes 1985b p 468!duplicates clusterDevoicing
define initialHomorgMerge    [ b b -> [b::2], g g -> [g::2], d d -> [d::2] || [.#.|%-] _ ] ; !from Rhodes 1985b p 469
define initialHomorgSonMerge [ m m -> [m::2], n n -> [n::2], w w -> [w::2] || [.#.|%-] _ ] ; !from Rhodes 1985b p 468
define nasalPlaceAssimilation       [m (->) [n::2] || _ [d|t] ,,
                                    m (->) [[n %']::2] || _ [z|s|g|k] ,,
                                    n (->) [m::2] || _ [b|p] ]; 

!!!!
define negHyphEpenthesis            [[..] (->) [%-::2] || _  [s|z] i i] ; 
define aposHyphDeletion             [[%'|%-] (->) [0::2] || \[%'|%-] _ \[%'|%-]] ; 
define nkApos                       [[..] (->) [%'::2] || n _ k ] ; 

!!!! Al rules
define gratuitousNH                 [y (->) [h ::2]|| n _ ,, [..] (->) h || n _ s ] ; 
define kaw2kow                      [a (->) [o ::2]|| k _ w] ; 
!define y2h                          [y (->) [%'::2] || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
!define glotGlide                    [h (->) [w ::2] || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
define nonConsFluidity              [y (->) [[%'::2]|h::2|w::2], 
                                     h (->) [[w::2]|y::2], 
                                     w (->) [[%'::2]|h::2|y::2] || [a|e|i|o] _ [a|e|i|o] ] ;  !this combines the y2h, glotGlide, hw rules into one, with a bit more specificity for hw
define unrounding                   [o (->) [a ::2]|| \[o] _ \[o]] ; !this is a general dialectal feature (beyond the specific final syllable vowel lowering rule)
define ko2kwa                       [o (->) [w a::2], w a (->) [o::2] || [g|k|m] _ ] ; !I think I have this backwards. The original configuration is Kwa, which becomes o in Odawa


define errorUnion [ !cnjNasVowel|
        !finPostNasVoi|
        finDevoi|
        finLowering|
        nasalEmergence|
        !gnMetathesis|
        !initialGlideDeletion|
        !structuralW|
        !postYRaising|
        !indDeletion|
        !nDeletion|
        !vvnTovvnh|
        !glideBacknessAssimilation|
        !preGlideLengthening|
        !wiCoalescence|
        !codaWDeletionMAYBEONSET|
        !hw| 
        !glottalWCluster| 
        stridentDevoicing|
        postStridentDevoicing|
        !clusterDevoicing|
        homorgObsMerge|
        lenisNasAssim|
        lenisStopDeletion|
        lenisFortition|
        initialHomorgMerge|
        initialHomorgSonMerge|
        nasalPlaceAssimilation|
        !negHyphEpenthesis|
        aposHyphDeletion|
        !nkApos|
        gratuitousNH| 
        !kaw2kow|
       ! y2h|
       ! glotGlide|
        nonConsFluidity|
        unrounding|
        ko2kwa !|
        !nVSzhAlternation|
        !nvw5oLEVEL|
        !shortening|
        !strandedNas
        ]; 

regex [errorUnion errorUnion] ; 
save stack errormodel_slim.hfst
