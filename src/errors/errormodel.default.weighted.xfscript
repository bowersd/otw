!define cnjNasVowel                  [[..] -> h ||  e n _ .#.] ; !redundant with nasalEmergence
!define finPostNasVoi                [p -> b, t -> d, k -> g, s -> z, s h -> z h, c h -> j   || [m|n]  _ .#. ]; !core devoicing rule no longer over applies after nasals, so we really only need the devoicing rule to cover the cases where post-nasal devoicing actually happens
define finDevoi                     [b -> [p::1], d -> [t::1], g -> [k::1], z -> [s::1], z h -> [s h::1], j -> [c h::1]   ||  _ .#. ]; 
![b -> p || m _ .#. ,,
!                                    d -> t, g -> k, z -> s, z h -> s h, j -> c h || n _ .#. ]  ; 
define finLowering                  [o -> [a::1], i -> [e::3] || \[a|i|o|e] _  (p|t|k|c h|
                                                                        [s (h)](p|t|k)|
                                                                        [m (b)]|[n (d|g|j|z (h)|h)]|
                                                                        w|y|%'|h) .#. ] ;
!!!!
define nasalEmergence               [h -> [0::2] || [a a|i i|e|o o] n _ .#.] ; 
define gnMetathesis                 [g n (->) [[n g]::2] || _ \[a|e|i|o]] ;
define indDeletion                  [d -> [0::2] || i n _ .#.] ;
define nDeletion                    [n -> [0::1] ||  _ .#.] ;

!!!!
define initialGlideDeletion         [y -> [0::2] || .#. _ [i|e],,
                                    w -> [0::2] || .#. _ o] ;
define postYRaising                 [a (->) [i::1] || y _ \[a]] ; 
define glideBacknessAssimilation    [w (->) [y::1] || [a|e|i] _ ,,
                                    y (->) [w::1] || o _ ] ; 
define preGlideLengthening          [[..] (->) [o::2] || o _ w ,,
                                     [..] (->) [i::2] || i _ y]; !there may also be some ambiguity before nasals
define wiCoalescence                [w i (->) [o::1]] ; 
define codaWDeletion                [w (->) [0::1] || _ \[a|i|o|e]];
define hw                           [h (->) [w|%'|0] || \[c|s|n|z] _ ];

define nVSzhAlternation             [z h (->) [n::2] || _ .#., _ i ] ; 

!!!! Voice in clusters
define stridentDevoicing            [z (->) [s::2] || _ (h) [p|t|k|b|d|g]] ; 
define postStridentDevoicing        [b (->) [p::2], d (->) [t::2], g (->) [k::2] || [s (h)] _ ] ; 
define clusterDevoicing             [b (->) [p::2], d (->) [t::2], z (->) [s::2], j (->) [[c h]::2], z h (->) [[s h]::2] || _ [p|t|k|s|c]] ;  

!!!! Cluster adjustments
define homorgObsMerge [
                    b b (->) [p::2], b p (->) [p::2], p b (->) [p::2], p p (->) [p::2],
                    d d (->) [t::2], d t (->) [t::2], t d (->) [t::2], t t (->) [t::2],
                    g g (->) [k::2], g k (->) [k::2], k g (->) [k::2], k k (->) [k::2],
                    z z (->) [s::2], z s (->) [s::2], s z (->) [s::2], s s (->) [s::2],
                    z h z h (->) [[s h]::2], z h s h (->) [[s h]::2], s h z h (->) [[s h]::2], sh sh (->) [[s h]::2],
                    j j (->) [[c h]::2], j c h (->) [[c h]::2], c h j (->) [[c h]::2], c h c h (->) [[c h]::2]
                    ]; 
define lenisNasAssim [ b (->) [m::2], g (->) [[n g]::2], d (->) [n::2] ||  _ [m|n] ,, !from Rhodes 1985b p 468
                       b n (->) [m::1] || _ d ] ; !If I had a dime for every bndam -> mdam...
define lenisStopDeletion [[d|b|g] (->) [0::2] || _ \[a|i|o|w|y]] ; 
define lenisFortition [ b (->) [p::2], g (->) [k::2], d (->) [t::2], j (->) [[c h]::2], z h (->) [[s h]::2], z (->) [s::2] ||  _ [p|t|k|s|c h] ] ; !from Rhodes 1985b p 468
define initialHomorgMerge    [ b b -> [b::2], g g -> [g::2], d d -> [d::2] || [.#.|%-] _ ] ; !from Rhodes 1985b p 469
define initialHomorgSonMerge [ m m -> [m::2], n n -> [n::2], w w -> [w::2] || [.#.|%-] _ ] ; !from Rhodes 1985b p 468
define nasalPlaceAssimilation       [m (->) [n::1] || _ [d|t|z|s|g|k] ,,
                                    m (->) [[n %']::1] || _ [z|s|g|k] ,,
                                    n (->) [m::1] || _ [b|p] ]; 

!!!!
define negHyphEpenthesis            [[..] (->) [%-::2] || _  [s|z] i i] ; 
define aposHyphDeletion             [[%'|%-] (->) [0::1]] ; 
define nkApos                       [[..] (->) [%'::2] || n _ k ] ; 

!!!! Al rules
define gratuitousNH                 [y (->) [h::1] || n _ ,, [..] (->) h || n _ s ] ; 
define kaw2kow                      [a (->) [o::1] || k _ w] ; 
define y2h                          [y (->) [%'::1] || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
define glotGlide                    [h (->) [w::1]  || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
define unrounding                   [o (->) [a::1] || \[o] _ , _ \[o]] ; !this is a general dialectal feature (beyond the specific final syllable vowel lowering rule)
define ko2kwa                       [o (->) [[w a]::1] || [g|k] _ ] ; !I think I have this backwards. The original configuration is Kwa, which becomes o in Odawa
define nvw5oLEVEL                   [[[\[a]a]|[\[i]i]|[\[o]o]] w a (->) [[o o]::2] || _  [n|g] .#. ] ; !DAB added for Al Corbiere. capturing a leveling found in Al Corbiere texts: baatiiniwag>baatiinoog, zhgi'gaazowag > zhgi'gaazoog
define shortening [i (->) [[i i]::2] || \[i] _ [g|n|n g] .#. ,, !variation in vowel length before pl/obv/loc
                o (->) [[o o]::2] || \[o] _ [g|n|n g] .#. ,,
                i i (->) [i::2] , o o (->) [o::2] || _ [g|n|n g] .#.
                ] ; 


define errorUnion [ !cnjNasVowel|
        !finPostNasVoi|
        finDevoi|
        finLowering|
        nasalEmergence|
        gnMetathesis|
        initialGlideDeletion|
        postYRaising|
        indDeletion|
        nDeletion|
        glideBacknessAssimilation|
        preGlideLengthening|
        wiCoalescence|
        codaWDeletion|
        hw| 
        stridentDevoicing|
        postStridentDevoicing|
        clusterDevoicing|
        homorgObsMerge|
        lenisNasAssim|
        lenisStopDeletion|
        lenisFortition|
        initialHomorgMerge|
        !initialHomorgSonMerge| !I added weights and now it doesn't work. took the weights out of this rule, and it still doesn't work
        nasalPlaceAssimilation|
        negHyphEpenthesis|
        aposHyphDeletion|
        nkApos|
        gratuitousNH|
        kaw2kow|
        y2h|
        !glotGlide| I added weights and now it doesn't work
        unrounding|
        ko2kwa|
        nVSzhAlternation|
        nvw5oLEVEL|
        shortening
        ]; 

regex [errorUnion errorUnion] ; 
save stack errormodel.hfst
