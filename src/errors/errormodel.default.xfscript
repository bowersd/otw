set encode-weights ON
!define cnjNasVowel                  [[..] -> h ||  e n _ .#.] ; !redundant with nasalEmergence
!define finPostNasVoi                [p -> [b::3], t -> [d::3], k -> [g::3], s -> [z::3], s h -> [z h::3], c h -> [j::3]   || [m|n]  _ .#. ]; 
define finDevoi                     [b -> [p::3], d -> [t::3], g -> [k::3], z -> [s::3], z h -> [s h::3], j -> [c h::3]   ||  _ .#. ]; 
![b -> p || m _ .#. ,,
!                                    d -> t, g -> k, z -> s, z h -> s h, j -> c h || n _ .#. ]  ; 
define finLowering                  [ o -> a, i -> e || \[a|i|o|e] _  (p|t|k|c h|                   !apparently .#. does not work in hfst except in hfst-twolc hfst-lexc and hfst-compose-intersect (https://github.com/hfst/hfst/blob/1f9efa75a3309b4b8926778e513886cee3c7396a/BUGS.old#L19)
                                                                        b|d|g|j|z (h)|
                                                                        [s (h)] (p|t|k)|
                                                                        [m (b)]|[n (d|g|j|z (h)|h)]|
                                                                        w|y|%'|h) .#.  
                                                                        ];
define unrounding                   [o (->) [a ::3]|| \[o] _ \[o]] ; !this is a general dialectal feature (beyond the specific final syllable vowel lowering rule)

!!!!word/syllable final nasals
define indDeletion                  [d -> [[0]::2] || i n _ .#.] ;
define nasalEmergence               [h -> [[0]::3] || n _ .#.] ; 
define nDeletion                    [n -> [[0]::3], n h -> [[0]::4] ||  [a|i|o|e] _ .#.] ; !in imperative -ken forms, nDeletion or hyphen epenthesis may occur, but not both (rules are stopped from applying to the same portion of a string by the error model set up as of 08/05/2024 (we also saw this with hyphenation and nasal stranding... added a new sub-clause of the rule to counteract that)
define vvnTovvnh                    [[..] -> [h::3] || [i i|a a|o o|e] n _ .#. ,,
                                     [..] -> [n h::4] || [i i|a a|o o|e] _ .#.] ; 
define gratuitousNH                 [y (->) [h ::3]|| n _ ,, [..] (->) h || n _ s ] ; 


!!!!glide-vowel
define wiCoalescence                [w i (->)  [o::3] || [.#.|\[a|i|o|e]] _ ] ; !should this be post-consonantal wi?
define postYRaising                 [a (->) [i::1] || y _ \[a]] ; 
define preGlideLengthening          [[..] (->) [o::3] || o _ w ,,
                                     [..] (->) [i::3] || i _ y]; !there may also be some ambiguity before nasals
define kaw2kow                      [a (->) [o ::3]|| k _ w] ; 
define ko2kwa                       [[h|%'] (->) [w::3], o (->) [w a::3], w a (->) [o::3] || [g|k|m] _ [\[o]|.#.]] ; !I think I have this backwards. The original configuration is Kwa, which becomes o in Odawa !updated to allow a stranded w. I think it should only be able to persist when the following consonant was weak enough to deletei (like a glottal). getting a w should only be an option when an o is deleted, but that deletion is in the phonology and so we just have to allow it everywhere

!!!!glides
!define y2h                          [y (->) [%'::3] || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
!define glotGlide                    [h (->) [w ::3] || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
define initialGlideDeletion         [y -> [0::3] || .#. _ [i|e],,
                                    w -> [0::3] || .#. _ o] ;
define structuralW                  [[..] -> [w::3] || .#. _ \[e|a|i|o|p|t|k|s (h)] (w) [e|a|i|o] ] ; 
define glideBacknessAssimilation    [[%'|h|w] (->) [y::3] || [a|e|i] _ ,,
                                    [%'|h|y] (->)  [w::3] || o _ ] ; 
define nonConsFluidity              [y (->) [[%'::2]|h::2|w::2], 
                                     h (->) [[w::2]|y::2],  !corbiere specific version below with apostrophes instead of h. 
                                     w (->) [[%'::2]|h::2|y::2] || [a|e|i|o] _ [a|e|i|o] 
                                      ] ;  !this combines the y2h, glotGlide, hw rules into one, with a bit more specificity for hw
define codaWDeletionMAYBEONSET      [w (->) [0::3], y (->) [0::3] || _ [b|d|g|p|t|k|z|s|j|c|m|n],,
                                     w (->) [0::4], y (->) [0::3] || _ [a|i|o|e]
                                    ];
define glottalWCluster              [[..] -> h  || [a|e|i|o] [\[c|s|n|z]|[[c|s|n|z] h]] _ w,   !preceding even parity
                                                   [a|e|i|o] _ w \[a|e|i|o]                    !preceding odd parity
                                    ] ; !Texts in the Odawa Language Project have hw clusters on the surface (which Piggott and Grafstein were criticized for having these in their dictionary by Rhodes 1985b:462). these clusters are eliminated by the phonological module here. No weight because this should come for free
!define hw                           [h (->) [[w|%'|0]::3] || \[c|s|n|z] _ ];


!!!! Voice in clusters
define stridentDevoicing            [z h (->) [s h::3], z (->) [s::3] || _ [p|t|k|b|d|g],,
                                        z h b (->) [s h p::4] ,, z h d (->) [s h t::4] ,, z h g (->) [s h k::4] ,,
                                        z b (->) [s p::4] ,, z d (->) [s t::4] ,, z g (->) [s k::4] 
                                        ] ; 
define postStridentDevoicing        [b (->) [p::3], d (->) [t::3], g (->) [k::3] || [s (h)] _ ] ; 
define clusterDevoicing             [b (->) [p::3], d (->) [t::3], z (->) [s::3], j (->) [c h::3], z h (->) [s h::3] || _ [p|t|k|s|c]] ;  

!!!! Cluster adjustments
define homorgObsMerge [
                    b b (->) [p::3], b p (->) [p::3], p b (->) [p::3], p p (->) [p::3],
                    d d (->) [t::3], d t (->) [t::3], t d (->) [t::3], t t (->) [t::3],
                    g g (->) [k::3], g k (->) [k::3], k g (->) [k::3], k k (->) [k::3],
                    z z (->) [s::3], z s (->) [s::3], s z (->) [s::3], s s (->) [s::3],
                    z h z h (->) [s h::3], z h s h (->) [s h::3], s h z h (->) [s::3], sh sh (->) [s h::3],
                    j j (->) [c h::3], j c h (->) [c h::3], c h j (->) [c h::3], c h c h (->) [c h::3]
                    ]; 
define lenisNasAssim [ b (->) [m::3], g (->) [n g::3], d (->) [n::3] ||  _ [m|n] ,, !from Rhodes 1985b p 468
                       b n (->) [m::3] || _ d ] ; !If I had a dime for every bndam -> mdam...
define lenisStopDeletion [[d|b|g] (->) [0::3] ||  _ [[b|d|g|p|t|k|m (b)|n (d|[z (h)]|j)|[[z (h)]]|[[s] (h) (p|t|k)]|j|c h] (w|y)] [a|i|o|e]] ; 
define lenisFortition [ b (->) [p::3], g (->) [k::3], d (->) [t::3], j (->) [c h::3], z h (->) [s h::3], z (->) [s::3] ||  _ [p|t|k|s|c h] ] ; !from Rhodes 1985b p 468
define initialHomorgMerge    [ b b -> [b::2], g g -> [g::2], d d -> [d::2] || [.#.|%-] _ ] ; !from Rhodes 1985b p 469
define initialHomorgSonMerge [ m m -> [m::2], n n -> [n::2], w w -> [w::2] || [.#.|%-] _ ] ; !from Rhodes 1985b p 468
define gnMetathesis                 [g n (->) [n g::3] || _ \[a|e|i|o]] ;
define nasalPlaceAssimilation       [m (->) [n::3] || _ [d|t|g|k|z|s] ,,
                                    m (->) [[n %']::3] || _ [z|s|g|k] ,,
                                    n (->) [m::3] || _ [b|p] ]; 
define strandedNas [[n|m|w|y] (->) [0::3] || [.#.|\[a|e|i|o]]  _ \[a|e|i|o] ]; !rule deletes m|n even when there is a following glide (even though with an approximant following the cues to n might be less likely to be swallowed). This is done because we are now deleting stranded glides as well, and they definitely can be stranded with a neighboring glide. Could of course make there be two rules

!!!!punctuation
define negAllomorphs                    [ i i|
                                                i k .#.|
                                                (i) g|
                                                (i) n o o|
                                                (i)  n o k .#. |
                                                i   n (%') g |
                                                    n o    g |
                                                i   n o    g |
                                                (i) w [a a|e] | 
                                                i   w   n |
                                                    w a n |
                                                i   w a n
                                                ] ; 
define negHyphEpenthesis            [[..] (->) [%-::3] || _  [[[s|z] negAllomorphs]|d a a|k e], [.#.|%-] [[[n g o d|n i i z h] (w a a s)]|n s|n i i (w)|n a a n|n s h w a a s|z h a a n g s|m i d a a s] _ !,,
                                        ![m|n] (->) [%-::3] ||  _  [z  negAllomorphs]
                                        ] ; !allowing  stranded nasals (and semi stranded ones) to turn into a hyphen, because we can't get nasal stranding and hyphenation to happen at the same place as independent processes.
define aposHyphDeletion             [
                                        [h (->) [0::3] || [b|d|g|p|t|k|m|w|y|h|j] _, \[c|s|n|z] _ [b|d|g|p|t|k|m|n|z|s|c|j|w|y|h]] .o.
                                        [%'|%-] (->) [0::3] || \[%'|%-] _ \[%'|%-]] ; 
define nkApos                       [[..] (->) [%'::3] || n _ k ] ; 

!!!! morphophonological mishaps
define nVSzhAlternation             [z h (->) [n::3] || \[%-] _ .#., \[%-] _ i ] ; 
define nvw5oLEVEL                   [n w a (->) [n o o::3] , [a|i|o] w a (->) [o o::3] || _  [n|g] .#. ] ; !DAB added for Al Corbiere. capturing a leveling found in Al Corbiere texts: baatiiniwag>baatiinoog, zhgi'gaazowag > zhgi'gaazoog. This isn't just an Al thing. it shows up in a text in Rand's corpus too (for baatiini/o). the first case is there to handle times where the post-consonantal vowel syncopated, but it is kept very specific because I am not sure how often this error happens
define shortening [i (->)   [i i::2] || \[i] _ [g|n|n g] .#. ,, !variation in vowel length before pl/obv/loc
                   o (->)   [o o::2] || \[o] _ [g|n|n g] .#. ,,
                   i i (->) [i ::2], o o (->) o || _ [g|n|n g] .#.
                ] ; 


define errorUnion [ !cnjNasVowel|
        !finPostNasVoi|
        finDevoi|
        finLowering|
        nasalEmergence|
        gnMetathesis|
        initialGlideDeletion|
        structuralW|
        postYRaising|
        indDeletion|
        vvnTovvnh|
        glideBacknessAssimilation|
        preGlideLengthening|
        wiCoalescence|
        codaWDeletionMAYBEONSET|
        !hw| 
        !glottalWCluster| 
        stridentDevoicing|
        postStridentDevoicing|
        clusterDevoicing|
        homorgObsMerge|
        lenisNasAssim|
        lenisStopDeletion|
        lenisFortition|
        initialHomorgMerge|
        initialHomorgSonMerge|
        nasalPlaceAssimilation|
        [strandedNas|
        nDeletion .o.
        negHyphEpenthesis]|
        aposHyphDeletion|
        nkApos|
        gratuitousNH| 
        kaw2kow|
       ! y2h|
       ! glotGlide|
        nonConsFluidity|
        unrounding|
        ko2kwa|
        nVSzhAlternation|
        nvw5oLEVEL|
        shortening
        ]; 

regex [errorUnion errorUnion] ; 
save stack errormodel.hfst
clear stack
define errorSlim [ errorUnion - 
        [
        gnMetathesis|
        initialGlideDeletion|
        structuralW|
        postYRaising|
        indDeletion|
        vvnTovvnh|
        glideBacknessAssimilation|
        preGlideLengthening|
        wiCoalescence|
        codaWDeletionMAYBEONSET|
        stridentDevoicing|
        postStridentDevoicing|
        clusterDevoicing|
        nkApos|
        kaw2kow|
        unrounding|
        ko2kwa !|
        nVSzhAlternation|
        nvw5oLEVEL|
        shortening
        ]
        ]; 

regex [errorSlim errorSlim] ; 
save stack errormodel_slim.hfst
clear stack

define errorCorbiere [errorUnion - nonConsFluidity
        ]; 

define nonConsFluidity              [y (->) [[%'::2]|[h::2]|[w::2]], 
                                     %' (->) [[w::2]|[y::2]|[h::2]],  !corbiere specific version that has apostrophes instead of h. test kinwaajbiiyan being mapped to corbiere-style kinwaajbii'an
                                     w (->) [[%'::2]|h::2|y::2] || [a|e|i|o] _ [a|e|i|o] ] ;  !this combines the y2h, glotGlide, hw rules into one, with a bit more specificity for hw

define errorCorbiere [errorCorbiere|nonConsFluidity] ; 
regex [errorCorbiere errorCorbiere] ; 
save stack errormodel_corbiere.hfst
