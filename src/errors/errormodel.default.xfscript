define cnjNasVowel                  [[..] -> h ||  e n _ .#.] ; 
define finDevoi                     [b -> p, d -> t, g -> k, z -> s, z h -> s h, j -> c h   ||  _ .#. ]; 
![b -> p || m _ .#. ,,
!                                    d -> t, g -> k, z -> s, z h -> s h, j -> c h || n _ .#. ]  ; 
define finLowering                  [o -> a, i -> e || [g|k|[s|z] (h)|p|b|m|n|w|%'] _  (p|t|k|c h|
                                                                        [s (h)](p|t|k)|
                                                                        [m (b)]|[n (d|g|j|z (h)|h)]|
                                                                        w|y|%') .#. ] 
!!!!
define nasalEmergence               [h -> 0 || n _ .#.] ; 
define gnMetathesis                 [g n (->) n g || _ \[a|e|i|o]] ;
define indDeletion                  [d -> 0 || i n _ .#.] ;

!!!!
define initialGlideDeletion         [y -> 0 || .#. _ [i|e],,
                                    w -> 0 || .#. _ o] ;
define postYRaising                 [a (->) i || y _ \[a]] ; 
define glideBacknessAssimilation    [w (->) y || [a|e|i] _ ,,
                                    y (->) w || o _ ] ; 
define preGlideLengthening          [[..] (->) o || o _ w ,,
                                     [..] (->) i || i _ y]; !there may also be some ambiguity before nasals
define wiCoalescence                [w i (->) o] ; 
define codaWDeletion                [w (->) 0 || _ \[a|i|o|e]];
define hw                           [h (->) [w|%'|0] || \[c|s|n|z] _ ];


!!!! Voice in clusters
define stridentDevoicing            [z h (->) s h, z (->) s || _ [p|t|k|b|d|g]] ; 
define postStridentDevoicing        [b (->) p, d (->) t, g (->) k || [s (h)] _ ] ; 
define clusterDevoicing             [b (->) p, d (->) t, z (->) s, j (->) c h, z h (->) s h || _ [p|t|k|s|c]] ;  

!!!! Cluster adjustments
define homorgObsMerge [
                    b b (->) p, b p (->) p, p b (->) p, p p (->) p,
                    d d (->) t, d t (->) t, t d (->) t, t t (->) t,
                    g g (->) k, g k (->) k, k g (->) k, k k (->) k,
                    z z (->) s, z s (->) s, s z (->) s, s s (->) s,
                    z h z h (->) s h, z h s h (->) s h, s h z h (->) s, sh sh (->) s h,
                    j j (->) c h, j c h (->) c h, c h j (->) c h, c h c h (->) c h
                    ]; 
define lenisNasAssim [ b (->) [m], g (->) [n g], d (->) [n] ||  _ [m|n] ,, !from Rhodes 1985b p 468
                       b n (->) m || _ d ] ; !If I had a dime for every bndam -> mdam...
define lenisStopDeletion [[d|b|g] (->) [0] || _ \[a|i|o|w|y]] ; 
define lenisFortition [ b (->) [p], g (->) [k], d (->) [t], j (->) [c h], z h (->) [s h], z (->) [s] ||  _ [p|t|k|s|c h] ] ; !from Rhodes 1985b p 468
define initialHomorgMerge    [ b b -> [b], g g -> [g], d d -> [d] || [.#.|%-] _ ] ; !from Rhodes 1985b p 469
define initialHomorgSonMerge [ m m -> [m], n n -> [n], w w -> [w] || [.#.|%-] _ ] ; !from Rhodes 1985b p 468
define nasalPlaceAssimilation       [m (->) n || _ [d|t] ,,
                                    m (->) [n %'] || _ [z|s|g|k] ,,
                                    n (->) m || _ [b|p] ]; 

!!!!
define negHyphEpenthesis            [[..] (->) %- || _  [s|z] i i] ; 
define aposHyphDeletion             [[%'|%-] (->) 0] ; 
define nkApos                       [[..] (->) %' || n _ k ] ; 

!!!! Al rules
define gratuitousNH                 [y (->) h || n _ ,, [..] (->) h || n _ s ] ; 
define kaw2kow                      [a (->) o || k _ w] ; 
define y2h                          [y (->) %' || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
define glotGlide                    [h (->) w  || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
define unrounding                   [o (->) a || \[o] _ , _ \[o]] ; !this is a general dialectal feature (beyond the specific final syllable vowel lowering rule)
define ko2kwa                       [o (->) w a || [g|k] _ ] ; !I think I have this backwards. The original configuration is Kwa, which becomes o in Odawa
define nvw5oLEVEL                   [[[\[a]a]|[\[i]i]|[\[o]o]] w a (->) o o || _  [n|g] .#. ] ; !DAB added for Al Corbiere. capturing a leveling found in Al Corbiere texts: baatiiniwag>baatiinoog, zhgi'gaazowag > zhgi'gaazoog
define shortening [i (->) i i || \[i] _ [g|n|n g] .#.,, !variation in vowel length before pl/obv/loc
                o (->) o o || \[o] _ [g|n|n g] .#.,,
                i i (->) i , o o (->) o || _ [g|n|n g] .#.
                ] ; 


define errorUnion [cnjNasVowel|
        finDevoi|
        finLowering|
        nasalEmergence|
        gnMetathesis|
        initialGlideDeletion|
        posYRaising|
        glideBacknessAssimilation|
        preGlideLengthening|
        wiCoalescence|
        codaWDeletion|
        hw| 
        stridentDevoicing|
        postStridentDevoicing|
        clusterDevoicing|
        homorgObsMerge|
        lenisNasAssim|
        lenisStopDeletion|
        lenisFortition|
        initialHomorgMerge|
        initialHomorgSonMerge|
        nasalPlaceAssimilation|
        negHyphEpenthesis|
        aposHyphDeletion|
        nkApos|
        gratuitousNH|
        kaw2kow|
        y2h|
        glotGlide|
        unrounding|
        ko2kwa|
        nvw5oLEVEL|
        shortening
        ]; 

regex [errorUnion errorUnion] ; 
save stack errormodel.hfst
