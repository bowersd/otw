set encode-weights ON
!define cnjNasVowel                  [[..] -> h ||  e n _ .#.] ; !redundant with nasalEmergence
define finPostNasVoi                [p -> [b::2], t -> [d::2], k -> [g::2], s -> [z::2], s h -> [z h::2], c h -> [j::2]   || [m|n]  _ .#. ]; 
define finDevoi                     [b -> [p::2], d -> [t::2], g -> [k::2], z -> [s::2], z h -> [s h::2], j -> [c h::2]   ||  _ .#. ]; 
![b -> p || m _ .#. ,,
!                                    d -> t, g -> k, z -> s, z h -> s h, j -> c h || n _ .#. ]  ; 
define finLowering                  [o -> a, i -> e || \[a|i|o|e] _  (p|t|k|c h|
                                                                        b|d|g|j|z (h)|
                                                                        [s (h)](p|t|k)|
                                                                        [m (b)]|[n (d|g|j|z (h)|h)]|
                                                                        w|y|%'|h) .#. ] ;
!!!!
define nasalEmergence               [h -> [[0]::2] || n _ .#.] ; 
define gnMetathesis                 [g n (->) [n g::2] || _ \[a|e|i|o]] ;
define indDeletion                  [d -> [[0]::1] || i n _ .#.] ;
define nDeletion                    [n -> [[0]::2] ||  [a|i|o|e] _ .#.] ;
define vvnTovvnh                    [[..] -> [h::2] || [i i|a a|o o|e] n _ .#.,,
                                     [..] -> [n h::3] || [ii|aa|oo|e] _ .#.] ; 

!!!!
define initialGlideDeletion         [y -> [0::2] || .#. _ [i|e],,
                                    w -> [0::2] || .#. _ o] ;
define structuralW                  [[..] -> [w::2] || .#. _ \[e|a|i|o|p|t|k|s (h)] (w) [e|a|i|o] ] ; 
define postYRaising                 [a (->) [i::1] || y _ \[a]] ; 
define glideBacknessAssimilation    [w (->) [y::2] || [a|e|i] _ ,,
                                    y (->)  [w::2] || o _ ] ; 
define preGlideLengthening          [[..] (->) [o::2] || o _ w ,,
                                     [..] (->) [i::2] || i _ y]; !there may also be some ambiguity before nasals
define wiCoalescence                [w i (->)  [o::2] || [.#.|\[a|i|o|e]] _ ] ; !should this be post-consonantal wi?
define codaWDeletionMAYBEONSET      [w (->) [0::2], y (->) [0::2] || _ [b|d|g|p|t|k|z|s|j|c|m|n],,
                                     w (->) [0::3] || _ [a|i|o|e]
                                    ];
define glottalWCluster              [[..] -> h  || [a|e|i|o] [\[c|s|n|z]|[[c|s|n|z] h]] _ w,   !preceding even parity
                                                   [a|e|i|o] _ w \[a|e|i|o]                    !preceding odd parity
                                    ] ; !Texts in the Odawa Language Project have hw clusters on the surface (which Piggott and Grafstein were criticized for having these in their dictionary by Rhodes 1985b:462). these clusters are eliminated by the phonological module here. No weight because this should come for free
!define hw                           [h (->) [[w|%'|0]::2] || \[c|s|n|z] _ ];

define nVSzhAlternation             [z h (->) [n::2] || \[%-] _ .#., \[%-] _ i ] ; 

!!!! Voice in clusters
define stridentDevoicing            [z h (->) [s h::2], z (->) [s::2] || _ [p|t|k|b|d|g]] ; 
define postStridentDevoicing        [b (->) [p::2], d (->) [t::2], g (->) [k::2] || [s (h)] _ ] ; 
define clusterDevoicing             [b (->) [p::2], d (->) [t::2], z (->) [s::2], j (->) [c h::2], z h (->) [s h::2] || _ [p|t|k|s|c]] ;  

!!!! Cluster adjustments
define homorgObsMerge [
                    b b (->) [p::2], b p (->) [p::2], p b (->) [p::2], p p (->) [p::2],
                    d d (->) [t::2], d t (->) [t::2], t d (->) [t::2], t t (->) [t::2],
                    g g (->) [k::2], g k (->) [k::2], k g (->) [k::2], k k (->) [k::2],
                    z z (->) [s::2], z s (->) [s::2], s z (->) [s::2], s s (->) [s::2],
                    z h z h (->) [s h::2], z h s h (->) [s h::2], s h z h (->) [s::2], sh sh (->) [s h::2],
                    j j (->) [c h::2], j c h (->) [c h::2], c h j (->) [c h::2], c h c h (->) [c h::2]
                    ]; 
define lenisNasAssim [ b (->) [m::2], g (->) [n g::2], d (->) [n::2] ||  _ [m|n] ,, !from Rhodes 1985b p 468
                       b n (->) [m::2] || _ d ] ; !If I had a dime for every bndam -> mdam...
define lenisStopDeletion [[d|b|g] (->) [0::2] ||  _ [[b|d|g|p|t|k|m (b)|n (d|[z (h)]|j)|[[z (h)]]|[[s] (h) (p|t|k)]j|c h] (w|y)] [a|i|o|e]] ; 
define lenisFortition [ b (->) [p::2], g (->) [k::2], d (->) [t::2], j (->) [c h::2], z h (->) [s h::2], z (->) [s::2] ||  _ [p|t|k|s|c h] ] ; !from Rhodes 1985b p 468
define initialHomorgMerge    [ b b -> [b::2], g g -> [g::2], d d -> [d::2] || [.#.|%-] _ ] ; !from Rhodes 1985b p 469
define initialHomorgSonMerge [ m m -> [m::2], n n -> [n::2], w w -> [w::2] || [.#.|%-] _ ] ; !from Rhodes 1985b p 468
define nasalPlaceAssimilation       [m (->) [n::2] || _ [d|t|g|k|z|s] ,,
                                    m (->) [[n %']::2] || _ [z|s|g|k] ,,
                                    n (->) [m::2] || _ [b|p] ]; 

!!!!
define negHyphEpenthesis            [[..] (->) [%-::2] || _  [s|z] i i] ; 
define aposHyphDeletion             [[%'|%-] (->) [0::2] || \[%'|%-] _ \[%'|%-]] ; 
define nkApos                       [[..] (->) [%'::2] || n _ k ] ; 

!!!! Al rules
define gratuitousNH                 [y (->) [h ::2]|| n _ ,, [..] (->) h || n _ s ] ; 
define kaw2kow                      [a (->) [o ::2]|| k _ w] ; 
!define y2h                          [y (->) [%'::2] || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
!define glotGlide                    [h (->) [w ::2] || [a|e|i|o] _ [a|e|i|o] ] ;  !this is similar to hw rule
define nonConsFluidity              [y (->) [[%'::1]|h::1|w::1], 
                                     h (->) [[w::1]|y::1],  !corbiere specific version below with apostrophes instead of h. 
                                     w (->) [[%'::1]|h::1|y::1] || [a|e|i|o] _ [a|e|i|o] ] ;  !this combines the y2h, glotGlide, hw rules into one, with a bit more specificity for hw
define unrounding                   [o (->) [a ::2]|| \[o] _ \[o]] ; !this is a general dialectal feature (beyond the specific final syllable vowel lowering rule)
define ko2kwa                       [o (->) [w a::2], w a (->) [o::2] || [g|k|m] _ ] ; !I think I have this backwards. The original configuration is Kwa, which becomes o in Odawa
define nvw5oLEVEL                   [n w a (->) [n o o::2] , [a|i|o] w a (->) [o o::2] || _  [n|g] .#. ] ; !DAB added for Al Corbiere. capturing a leveling found in Al Corbiere texts: baatiiniwag>baatiinoog, zhgi'gaazowag > zhgi'gaazoog. This isn't just an Al thing. it shows up in a text in Rand's corpus too (for baatiini/o). the first case is there to handle times where the post-consonantal vowel syncopated, but it is kept very specific because I am not sure how often this error happens
define shortening [i (->)   [i i::1] || \[i] _ [g|n|n g] .#. ,, !variation in vowel length before pl/obv/loc
                   o (->)   [o o::1] || \[o] _ [g|n|n g] .#. ,,
                   i i (->) [i ::1], o o (->) o || _ [g|n|n g] .#.
                ] ; 
define strandedNas [[n|m] (->) [0::2] || \[a|e|i|o]  _ \[a|e|i|o|w|y] ]; !adding further restrictions on the thinking that with an approximant following the cues to n would be less likely to be swallowed


define errorUnion [ !cnjNasVowel|
        !finPostNasVoi|
        finDevoi|
        finLowering|
        nasalEmergence|
        gnMetathesis|
        initialGlideDeletion|
        structuralW|
        postYRaising|
        indDeletion|
        nDeletion|
        vvnTovvnh|
        glideBacknessAssimilation|
        preGlideLengthening|
        wiCoalescence|
        codaWDeletionMAYBEONSET|
        !hw| 
        !glottalWCluster| 
        stridentDevoicing|
        postStridentDevoicing|
        clusterDevoicing|
        homorgObsMerge|
        lenisNasAssim|
        lenisStopDeletion|
        lenisFortition|
        initialHomorgMerge|
        initialHomorgSonMerge|
        nasalPlaceAssimilation|
        negHyphEpenthesis|
        aposHyphDeletion|
        nkApos|
        gratuitousNH| 
        kaw2kow|
       ! y2h|
       ! glotGlide|
        nonConsFluidity|
        unrounding|
        ko2kwa|
        nVSzhAlternation|
        nvw5oLEVEL|
        shortening|
        strandedNas
        ]; 

regex [errorUnion errorUnion] ; 
save stack errormodel.hfst
clear stack
define errorSlim [ errorUnion - 
        [
        gnMetathesis|
        initialGlideDeletion|
        structuralW|
        postYRaising|
        indDeletion|
        nDeletion|
        vvnTovvnh|
        glideBacknessAssimilation|
        preGlideLengthening|
        wiCoalescence|
        codaWDeletionMAYBEONSET|
        stridentDevoicing|
        postStridentDevoicing|
        clusterDevoicing|
        negHyphEpenthesis|
        nkApos|
        kaw2kow|
        unrounding|
        ko2kwa !|
        nVSzhAlternation|
        nvw5oLEVEL|
        shortening|
        strandedNas
        ]
        ]; 

regex [errorSlim errorSlim] ; 
save stack errormodel_slim.hfst
clear stack

define errorCorbiere [errorUnion - nonConsFluidity
        ]; 

define nonConsFluidity              [y (->) [[%'::1]|[h::1]|[w::1]], 
                                     %' (->) [[w::1]|[y::1]|[h::1]],  !corbiere specific version that has apostrophes instead of h. test kinwaajbiiyan being mapped to corbiere-style kinwaajbii'an
                                     w (->) [[%'::1]|h::1|y::1] || [a|e|i|o] _ [a|e|i|o] ] ;  !this combines the y2h, glotGlide, hw rules into one, with a bit more specificity for hw

define errorCorbiere [errorCorbiere|nonConsFluidity] ; 
regex [errorCorbiere errorCorbiere] ; 
save stack errormodel_corbiere.hfst
