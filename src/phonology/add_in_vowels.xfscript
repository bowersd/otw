define vowels [a|i|o|e] ; 
define glides [w|y] ;
define glottals [h|%'] ; 
define lenisNonApproximants [b|d|g|m|n|j|z (h)] ;
define fortis [p|t|k|c h|s (h)] ; 

define wordInitialSingle  [lenisNonApproximants|glides] ; 
define wordInitialComplex [lenisNonApproximants glides] ;

define wordMedialSingle [lenisNonApproximants|fortis|glides|glottals] ;
define nasalCluster [
                        [m b]|
                        [n [d|j|z (h)|s|y]]
                    ] ; 
define stridentCluster [[s (h)] (p|t|k)];
define wordMedialComplex [
                            [[lenisNonApproximants|fortis] glides]|
                            [[nasalCluster|stridentCluster] (glides)]
                            ] ; 

define rmspecialv [
                    Va1 -> o ,
                    Vi1 -> a ,
                    Vv1 -> i ,
                    Vo1 -> w a ,
                    Vo2 -> a ,
                    Bv1 -> i 
                ];

define aposToV [%' -> [a|i|o] || \[a|i|o|e] _ \[a|i|o|e]] ; 
define mdToband [m d (->) b a n d] ; 
define initialVoicelessExpansion [
                                    p (->) [b [a|i|o] [b|p]]                                    || [.#.|%-|%<] _ ,,
                                    t (->) [d [a|i|o] [d|t]]                                    || [.#.|%-|%<] _ ,,
                                    k (->) [g [a|i|o] [g|k]]                                    || [.#.|%-|%<] _ ,,
                                    s (->) [z [a|i|o] [z|s]]                                    || [.#.|%-|%<] _ ,,
                                    s h (->) [[z h [a|i|o] [z|s] (h)]|[z (h) [a|i|o] [z|s] h]]  || [.#.|%-|%<] _ ,,
                                    c h (->) [j [a|i|o] [j|c h]]                                || [.#.|%-|%<] _ 
                                    ] ; 
 
define obligatoryVC [[..] -> [a|i|o] || [.#.|%-|%<] _ [p|t|k|s|h|%'|c h],
                                        [.#.|%-|%<] _ [b|d|g|m|n|z (h)|j|w|y] [a|i|o] \[a|i|o|%<|%-]
] ; 
define initialprimaryObligatorySiteVariation [
                                                [[..] -> V V || 
                                                [.#.|%-|%<] _ [[n [d|j|z (h)|g]]|m b] ,
                                                [.#.|%-|%<] [n] _ [d|j|z (h)|g],
                                                [.#.|%-|%<] [m] _ [b] ,
                                                [.#.|%-|%<] _ [b|d|g|m|n|z (h)|j] [w] [a|i|o] \[a|i|o] ,
                                                [.#.|%-|%<] [b|d|g|m|n|z (h)|j] _ [w] [a|i|o] \[a|i|o] 
                                                ] .o.
                                                [
                                                    V  V n V  V -> [[a|i|o] n]|[n [a|i|o]] ,
                                                    V  V m V  V -> [[a|i|o] m]|[m [a|i|o]] ,
                                                    V  V b V  V -> [[a|i|o] b]|[b [a|i|o]] ,
                                                    V  V d V  V -> [[a|i|o] d]|[d [a|i|o]] ,
                                                    V  V g V  V -> [[a|i|o] g]|[g [a|i|o]] ,
                                                    V  V z V  V -> [[a|i|o] z]|[z [a|i|o]] ,
                                                    V  V j V  V -> [[a|i|o] j]|[j [a|i|o]] ,
                                                    V  V z h V V -> [[a|i|o] z h]|[z h [a|i|o]] ]
                                                    ] ;
define initialprimaryOptionalSiteVariation [
                                                [[..] -> V V || 
                                                [.#.|%-|%<] _ [b|d|g|m|n|z (h)|j] [w] [a a|i i|o o] ,
                                                [.#.|%-|%<] [b|d|g|m|n|z (h)|j] _ [w] [a a|i i|o o] 
                                                ] .o.
                                                [
                                                    V V n V V   -> [n]  |[[a|i|o] n]  |[n [a|i|o]] ,,
                                                    V V m V V   -> [m]  |[[a|i|o] m]  |[m [a|i|o]] ,,
                                                    V V b V V   -> [b]  |[[a|i|o] b]  |[b [a|i|o]] ,,
                                                    V V d V V   -> [d]  |[[a|i|o] d]  |[d [a|i|o]] ,,
                                                    V V g V V   -> [g]  |[[a|i|o] g]  |[g [a|i|o]] ,,
                                                    V V z V V   -> [z]  |[[a|i|o] z]  |[z [a|i|o]] ,,
                                                    V V j V V   -> [j]  |[[a|i|o] j]  |[j [a|i|o]] ,,
                                                    V V z h V V -> [z h]|[[a|i|o] z h]|[z h [a|i|o]] ]
                                                    ] ;

define optionalVC [[..] (->) [a|i|o] || [.#.|%-|%<] _ [b|d|g|m|n|z (h)|j|w|y] [a a|i i|o o|e]] ; 
define primaryToCV [[..] -> [a|i|o] ||  [a|i|o|e] [m] _ [b] [a|i|o],
                                        [a|i|o|e] [n] _ [d|z (h)|j|y|h] [a|i|o],
                                        [a|i|o|e] [s (h)] _ [p|t|k] [a|i|o],
                                        [a|i|o|e] [b|d|g|p|t|k|m|n|[s|z] (h)|j|c h] _ [w] [a|i|o] 
                                        ]; !also optional V inside these clusters when sandwiched between long vowels, but there is precisely 0 ROI there 

define primaryWToCVCWorCCVW [
                                [..] -> V V ||  
                                        [a|i|o|e] [m] _ [b][w] [a|i|o],
                                        [a|i|o|e] [n] _ [d|z (h)|j][w] [a|i|o],
                                        [a|i|o|e] [s (h)] _ [p|t|k][w] [a|i|o],
                                        [a|i|o|e] [m]  [b] _ [w]  [a|i|o],
                                        [a|i|o|e] [n]  [d|z (h)|j] _ [w] [a|i|o],
                                        [a|i|o|e] [s (h)]  [p|t|k] _ [w] [a|i|o] .o.
                                        V V b V V -> [[a|i|o] b]|[b [a|i|o]],,
                                        V V d V V -> [[a|i|o] d]|[d [a|i|o]],,
                                        V V z V V -> [[a|i|o] z]|[z [a|i|o]],,
                                        V V z h V V -> [[a|i|o] z h]|[z h [a|i|o]],,
                                        V V p V V -> [[a|i|o] p]|[p [a|i|o]],,
                                        V V t V V -> [[a|i|o] t]|[t [a|i|o]],,
                                        V V k V V -> [[a|i|o] k]|[k [a|i|o]]
                                        ];

define primaryToCCV [[..] -> [a|i|o] ||
                                        [a|i|o|e] 
                                                 [m b|
                                                 n [d|j|z (h)|y|g]|
                                                 s (h) [p|t|k]
                                                 ] _ \[a|i|o|e|w|y],
                                        [a|i|o|e]
                                                 [b|d|g|
                                                 p|t|k|
                                                 [z|s] (h)|
                                                 m|n] [w|y]         _ \[a|i|o|e]
                                                                    ];

define secondaryToCV [[..] -> [a|i|o] || 
                                        !consonantal non-syllabics
                                        [a|i|o|e|%-|%<|.#.] [ 
                                                    b|d|g|
                                                    p|t|k|
                                                    z (h)|
                                                    j|c h
                                                    ]    _ [
                                                               b|d|g| 
                                                               p|t|k| 
                                                               z (h)| 
                                                               s (h)| 
                                                               m|n| 
                                                               j|c h| 
                                                               %'|h 
                                                               ] ,
                                          !non-consonantal non-syllabics
                                          [a|i|o|e|%-|%<|.#.] [ 
                                                     %'|h|
                                                     w|y
                                                     ]    _ [
                                                                b|d|g| 
                                                                p|t|k| 
                                                                z (h)| 
                                                                s (h)| 
                                                                m|n| 
                                                                j|c h| 
                                                                %'|h| 
                                                                w|y
                                                                ] ,
                                          !fortis stridents
                                          [a|i|o|e|%-|%<|.#.] [ s (h) ]    _ [ 
                                                                b|d|g| 
                                                                z (h)| 
                                                                s (h)| 
                                                                m|n| 
                                                                j|c h| 
                                                                %'|h| 
                                                                y
                                                                ] ,
                                          !m
                                          [a|i|o|e|%-|%<|.#.] [ m  ]    _ [ 
                                                                d|g| 
                                                                p|t|k| 
                                                                z (h)| 
                                                                s (h)| 
                                                                m|n| 
                                                                j|c h| 
                                                                %'|h| 
                                                                y
                                                                ] ,
                                          !n
                                          [a|i|o|e|%-|%<|.#.] [ n ]    _ [
                                                                b| 
                                                                p|t|k| 
                                                                m|n| 
                                                                c h| 
                                                                %'
                                                                ]
                                        ];

read regex [
    rmspecialv .o.
    aposToV .o.
    mdToband .o.
    initialVoicelessExpansion .o.
    obligatoryVC .o.
    initialprimaryObligatorySiteVariation .o.
    initialprimaryOptionalSiteVariation .o.
    optionalVC .o.
    primaryToCV .o.
    primaryWToCVCWorCCVW .o.
    primaryToCCV .o.
    secondaryToCV
] ;

save stack urCandidates.hfst
