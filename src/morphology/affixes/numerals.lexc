!ALL THE FINAL VOWELS HERE NEED TO SURFACE!!

LEXICON NUMERAL_PRE_FORMS
ngo+:nigo-                  NUMERAL_PRE_FORMATION ;
niizhoo+:niizhoo-                   NUMERAL_PRE_FORMATION ;
nso+:niso-                  NUMERAL_PRE_FORMATION ;
niiwoo+:niiwoo-                 NUMERAL_PRE_FORMATION ;
naano+:naano-                   NUMERAL_PRE_FORMATION ;
ngodwaasoo+:nigodwaasoo-                    NUMERAL_PRE_FORMATION ;
niizhwaasoo+:niizhwaasoo-                   NUMERAL_PRE_FORMATION ;
nshwaasoo+:nishwaasoo-                  NUMERAL_PRE_FORMATION ;
zhaangsoo+:zhaangasoo-                  NUMERAL_PRE_FORMATION ;
mdaaso+:mdaaso-                 NUMERAL_PRE_FORMATION ;

LEXICON NUMERAL_ROOTS_GATEWAY
        NUMERAL_ROOT_FORMS_ONE_TEN ; 
        NUMERAL_ROOT_FORMS_TWO_NINE ; 
        NUMERAL_STANDALONE_FORMS_TEN ;
        NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN ;
@R.NUMERAL.HUNDRED@@D.NUMERAL.TEN@  NUMERAL_HALF ; 

LEXICON NUMERAL_HALF
aabta:aabta     # ; 

LEXICON NUMERAL_ROOT_FORMS_ONE_TEN
ngod:ningod            NUMERAL_STEM_HUNDRED_GET "one"; !can't combine with mdana (10), can combine with waak (100)
mdaas:midaas          NUMERAL_STEM_HUNDRED_GET "ten"; 

LEXICON NUMERAL_ROOT_FORMS_TWO_NINE
niizh:niizh             NUMERAL_ROOT_FORMS_GET_TWO "two"; 
nis:nis                 NUMERAL_ROOT_FORMS_GET "three"; 
nii:nii                 NUMERAL_ROOT_FORMS_GET "four"; 
!naan+:naanan           NUMERAL_ROOT_FORMS_GET "five"; 
naan:naan               NUMERAL_ROOT_FORMS_GET "five"; 
ngodwaas:ningodwaas     NUMERAL_ROOT_FORMS_GET "six"; 
niizhwaas:niizhwaas     NUMERAL_ROOT_FORMS_GET "seven"; 
nshwaas:nishwaas        NUMERAL_ROOT_FORMS_GET "eight"; 
zhaangs:zhaangas        NUMERAL_ROOT_FORMS_GET "nine"; 

LEXICON NUMERAL_ROOT_FORMS_GET_TWO
@D.NUMERAL.TEN@         NUMERAL_ROOT_FORMS_TWO_TENS ; 
@D.NUMERAL.HUNDRED@     NUMERAL_STEM_HUNDRED_SET ; 

LEXICON NUMERAL_ROOT_FORMS_GET
@D.NUMERAL.TEN@         NUMERAL_STEM_TEN_SET ; 
@D.NUMERAL.HUNDRED@     NUMERAL_STEM_HUNDRED_SET ;

LEXICON NUMERAL_STEM_TEN_SET  
@P.NUMERAL.TEN@     NUMERAL_STEM_TEN ;

LEXICON NUMERAL_ROOT_FORMS_TWO_TENS
+mdana:%>dana     NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ; !need to trigger epenthesis when it is CC

LEXICON NUMERAL_STEM_TEN
+mdana:%>midana     NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ; !need to trigger epenthesis when it is CC

LEXICON NUMERAL_STEM_HUNDRED_GET
@D.NUMERAL.HUNDRED@ NUMERAL_STEM_HUNDRED_SET ;

LEXICON NUMERAL_STEM_HUNDRED_SET
@P.NUMERAL.HUNDRED@ NUMERAL_STEM_HUNDRED ;

LEXICON NUMERAL_STEM_HUNDRED
+waak:waak     NUMERAL_STEM_HUNDRED_GATEWAY ; 

LEXICON NUMERAL_STEM_HUNDRED_GATEWAY
            # ;
+shi+:-shi- NUMERAL_ROOTS_GATEWAY ; 

LEXICON NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN
bezhig:bezhig               # "one";
niizh:niizh                 # "two";
nswi:niswi                  # "three";
niiwin:niiwin               # "four";
naanan:naanan               # "five";
!naanin:naanin               # "five";
ngodwaaswi:ningodwaaswi     # "six";
niizhwaaswi:niizhwaasawi    # "seven";
nshwaaswi:nishwaaswi        # "eight";
zhaangswi:zhaangaswi        # "nine";

LEXICON NUMERAL_STANDALONE_FORMS_TEN !THIS NEEDS TO BE CONNECTED UPSTREAM
mdaaswi:mdaaswi             NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ;

LEXICON NUMERAL_STANDALONE_FORMS_TEN_GATEWAY 
    # ; 
+shi+:-ashi-    NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN ; !form numbers X1-X9
