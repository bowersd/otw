!ALL THE FINAL VOWELS HERE NEED TO SURFACE!!

LEXICON NUMERAL_PRE_FORMS
ngo+:nigo-                  NUMERAL_PRE_FORMATION ;
niizhoo+:niizhoo-                   NUMERAL_PRE_FORMATION ;
nso+:niso-                  NUMERAL_PRE_FORMATION ;
niiwoo+:niiwoo-                 NUMERAL_PRE_FORMATION ;
naano+:naano-                   NUMERAL_PRE_FORMATION ;
ngodwaasoo+:nigodwaasoo-                    NUMERAL_PRE_FORMATION ;
niizhwaasoo+:niizhwaasoo-                   NUMERAL_PRE_FORMATION ;
nshwaasoo+:nishwaasoo-                  NUMERAL_PRE_FORMATION ;
zhaangsoo+:zhaangasoo-                  NUMERAL_PRE_FORMATION ;
mdaaso+:mdaaso-                 NUMERAL_PRE_FORMATION ;

LEXICON NUMERAL_ROOTS_GATEWAY
        ORDINALS ; 
        NUMERAL_ROOT_FORMS_ONE ; 
        NUMERAL_ROOT_FORMS_TWO_NINE ; 
        NUMERAL_ROOT_FORMS_TEN ; 
        NUMERAL_STANDALONE_FORMS_TEN ;
        NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN ;
@R.NUMERAL.HUNDRED@@D.NUMERAL.TEN@  NUMERAL_HALF ; 

LEXICON ORDINALS
PV/e+ntam+Adv:e-ntam     # ; !first ordinal
PV/shki+ntam+Adv:shki-ntam     # ; !first ordinal
PV/aapji+ntam+Adv:aapji-ntam     # ; !first ordinal
PV/CCNJ+PV/ko+:eko-               ORDINALS_SET ;

LEXICON ORDINALS_SET
@P.NUMERAL.ORD@ NUMERAL_ROOT_FORMS_TWO_NINE ;  !used to ensure ordinals end in repetitions. currently repetitions are limited to simple roots, can't build a more complex numeral (and so it is also impossible to build a more complex ordinal)
@P.NUMERAL.ORD@ NUMERAL_ROOT_FORMS_TEN ; 


LEXICON NUMERAL_HALF
aabta:aabta     # "half"; 

LEXICON NUMERAL_ROOT_FORMS_ONE
ngod:ningod            NUMERAL_STEM_HUNDRED_GET "one"; !can't combine with mdana (10), can combine with waak (100). ordinal form is ntam (with just e-, not eko-)

LEXICON NUMERAL_ROOT_FORMS_TEN
mdaas:midaa          NUMERAL_ROOT_FORMS_TEN_GET "ten"; !mdaas everywhere except repetition forms (and thus ordinals as well, then mdaas/mdaach. can't combine with mdana, can combine with waak

LEXICON NUMERAL_ROOT_FORMS_TEN_GET
@D.NUMERAL.ORD@ NUMERAL_ROOT_FORMS_TEN_S ; 
                NUMERAL_ROOT_FORMS_TEN_S_CH ; 

LEXICON NUMERAL_ROOT_FORMS_TEN_S
0:s         NUMERAL_STEM_HUNDRED_GET ;

LEXICON NUMERAL_ROOT_FORMS_TEN_S_CH
0:s         NUMERAL_ROOT_FORMS_REPETITION ; 
0:ch        NUMERAL_ROOT_FORMS_REPETITION ; 

LEXICON NUMERAL_ROOT_FORMS_TWO_NINE
niizh:niizh             NUMERAL_ROOT_FORMS_GET_TWO "two";        !20 is niizhdana (no m), otherwise regular
nis:nis                 NUMERAL_ROOT_FORMS_35_GATEWAY "three";   !fully regular
nii:nii                 NUMERAL_ROOT_FORMS_FOUR_GATEWAY "four";  !nii everywhere, except in repetition form, then niiwing
!naan+:naanan           NUMERAL_ROOT_FORMS_35_GATEWAY "five";    !have not seen this variant, but it was mercurially listed in valentine 2001:874
naan:naan               NUMERAL_ROOT_FORMS_35_GATEWAY "five";    !fully regular
ngodwaas:ningodwaa     NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY "six"; !ngodwaas everywhere but repetition forms (and thus ordinals as well), then ngodwaach
niizhwaas:niizhwaa     NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY "seven"; !niizhwaas everywhere but repetition forms (and thus ordinals as well), then niizhwaach
nshwaas:nishwaa        NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY "eight"; !nshwaas everywhere but repetition forms (and thus ordinals as well), then nshwaach
zhaangs:zhaanga        NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY "nine"; !zhaangs everywhere but repetition forms (and thus ordinals as well), then zhaangch

LEXICON NUMERAL_ROOT_FORMS_35_GATEWAY
                    NUMERAL_ROOT_FORMS_REPETITION ; 
                    NUMERAL_ROOT_FORMS_GET ; 

LEXICON NUMERAL_ROOT_FORMS_FOUR_GATEWAY
0:w                 NUMERAL_ROOT_FORMS_REPETITION ; 
                    NUMERAL_ROOT_FORMS_GET ; 

LEXICON NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY
0:s                 NUMERAL_ROOT_FORMS_GET  ; 
0:ch                NUMERAL_ROOT_FORMS_REPETITION  ; 

LEXICON NUMERAL_ROOT_FORMS_REPETITION
+VII+Cnj+0:ing # ; 

LEXICON NUMERAL_ROOT_FORMS_GET_TWO
                                        NUMERAL_ROOT_FORMS_REPETITION ; 
@D.NUMERAL.ORD@@D.NUMERAL.TEN@         NUMERAL_ROOT_FORMS_TWO_TENS ; 
@D.NUMERAL.ORD@@D.NUMERAL.HUNDRED@     NUMERAL_STEM_HUNDRED_SET ; 

LEXICON NUMERAL_ROOT_FORMS_GET
@D.NUMERAL.ORD@@D.NUMERAL.TEN@         NUMERAL_STEM_TEN_SET ; 
@D.NUMERAL.ORD@@D.NUMERAL.HUNDRED@     NUMERAL_STEM_HUNDRED_SET ;

LEXICON NUMERAL_STEM_TEN_SET  
@P.NUMERAL.TEN@     NUMERAL_STEM_TEN ;

LEXICON NUMERAL_ROOT_FORMS_TWO_TENS
+mdana:%>dana     NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ; !need to trigger epenthesis when it is CC

LEXICON NUMERAL_STEM_TEN
+mdana:%>midana     NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ; !need to trigger epenthesis when it is CC

LEXICON NUMERAL_STEM_HUNDRED_GET
@D.NUMERAL.HUNDRED@ NUMERAL_STEM_HUNDRED_SET ;

LEXICON NUMERAL_STEM_HUNDRED_SET
@P.NUMERAL.HUNDRED@ NUMERAL_STEM_HUNDRED ;

LEXICON NUMERAL_STEM_HUNDRED
+waak:waak     NUMERAL_STEM_HUNDRED_GATEWAY ; 

LEXICON NUMERAL_STEM_HUNDRED_GATEWAY
            # ;
+shi+:-shi- NUMERAL_ROOTS_GATEWAY ; 

LEXICON DISTRIBUTIVE_REDUPLICATION
Dist+CRED1e     DISTRIBUTIVE_REDUPLICATION_GATEWAY

LEXICON DISTRIBUTIVE_REDUPLICATION_GATEWAY
        NUMERAL_STANDALONE_FORMS_LESS_THAN_10 ;
        NUMERAL_STANDALONE_FORMS_10 ;

LEXICON NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN
bezhig:bezhig               # "one";
niizh:niizh                 # "two";
nswi:niswi                  # "three";
niiwin:niiwin               # "four";
naanan:naanan               # "five";
!naanin:naanin               # "five";
ngodwaaswi:ningodwaaswi     # "six";
niizhwaaswi:niizhwaasawi    # "seven";
nshwaaswi:nishwaaswi        # "eight";
zhaangswi:zhaangaswi        # "nine";

LEXICON NUMERAL_STANDALONE_FORMS_TEN !THIS NEEDS TO BE CONNECTED UPSTREAM
mdaaswi:mdaaswi             NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ;

LEXICON NUMERAL_STANDALONE_FORMS_TEN_GATEWAY 
    # ; 
+shi+:-ashi-    NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN ; !form numbers X1-X9
