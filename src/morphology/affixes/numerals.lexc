
!LEXICON NUMERAL_PRE_FORMS
!PNum/ngo+:nigo-                              NUMERAL_PRE_FORMS_GATEWAY "one";
!PNum/niizhoo+:niizhoo-                       NUMERAL_PRE_FORMS_GATEWAY "two";
!PNum/nso+:niso-                              NUMERAL_PRE_FORMS_GATEWAY "three";
!PNum/niiwoo+:niiwoo-                         NUMERAL_PRE_FORMS_GATEWAY "four";
!PNum/naano+:naano-                           NUMERAL_PRE_FORMS_GATEWAY "five";
!PNum/ngodwaasoo+:ningodwaasoo-                NUMERAL_PRE_FORMS_GATEWAY "six (1+5)";
!PNum/niizhwaasoo+:niizhwaasoo-               NUMERAL_PRE_FORMS_GATEWAY "seven (2+5);
!PNum/nshwaasoo+:nishwaasoo-                  NUMERAL_PRE_FORMS_GATEWAY "eight (3+5);
!PNum/zhaangsoo+:zhaangasoo-                  NUMERAL_PRE_FORMS_GATEWAY "nine";
!PNum/mdaaso+:midaaso-                         NUMERAL_PRE_FORMS_GATEWAY "ten (?+5)";
!
!LEXICON NUMERAL_PRE_FORMS_GATEWAY
!!risky to run nouns and verbs through the same gateway. there are a lot of preverbs that would then be able to concatenate with nouns.
!!instead, just put the numerical preverbs/prenouns in the lexica for nouns and verbs separately. 
!        V_REDUPLICATION_STEM ; 
!        NOUNS ; 


LEXICON CARDINALS
ngod+Num:ningod     CARDINALS_HUNDREDS ;
niizh+Num:niizh     CARDINALS_HUNDREDS ;
nswi+Num:nis     CARDINALS_HUNDREDS ;
niiwin+Num:nii     CARDINALS_HUNDREDS ;
naanan+Num:naan     CARDINALS_HUNDREDS ;
ngodwaaswi+Num:ningodwaas     CARDINALS_HUNDREDS ;
niizhwaaswi+Num:niizhwaas     CARDINALS_HUNDREDS ;
nshwaaswi+Num:nishwaas   CARDINALS_HUNDREDS ;
zhaangswi+Num:zhaangas   CARDINALS_HUNDREDS ;
mdwaaswi+Num:midwaas    CARDINALS_HUNDREDS ;
                        CARDINALS_TENS ; 
                        CARDINALS_STANDALONE_ONE_NINE ; 
                        CARDINALS_STANDALONE_ONE_NINE ; 

LEXICON CARDINALS_HUNDREDS  
waak+Num:waak   CARDINALS_HUNDREDS_SHI ;

LEXICON CARDINALS_HUNDREDS_SHI
+shi+Conj+:-ashi-    CARDINALS_HUNDREDS_AABTA ; 
                    # ; 

LEXICON CARDINALS_HUNDREDS_AABTA
aabta+Num:aabita    CLOSED_PROTECTION ; 
                    CARDINALS_TENS ; 

LEXICON CARDINALS_TENS
!NO BEZHIG/NGOD HERE
niizh+Num+mdana+Num:niizhidana   CARDINALS_TENS_SHI; 
nswi+Num:nis                     CARDINALS_TENS_MIDANA; 
niiwin+Num:nii                   CARDINALS_TENS_MIDANA; 
naanan+Num:naan                  CARDINALS_TENS_MIDANA; 
ngodwaaswi+Num:ningodwaas        CARDINALS_TENS_MIDANA; 
niizhwaaswi+Num:niizhwaas        CARDINALS_TENS_MIDANA; 
nshwaaswi+Num:nishwaas           CARDINALS_TENS_MIDANA; 
zhaangswi+Num:zhaangas           CARDINALS_TENS_MIDANA; 
mdaaswi+Num:midaaswi             CARDINALS_TENS_SHI;
+shi+Conj+:-ashi-                 CARDINALS_ONE_NINE; !short form of -teen words
                                 CARDINALS_ONE_NINE; 

LEXICON CARDINALS_TENS_MIDANA
+mdana+Num:%>midana               CARDINALS_TENS_SHI; 


LEXICON CARDINALS_TENS_SHI
+shi+Conj+:-ashi-    CARDINALS_ONE_NINE ;
                    CLOSED_PROTECTION ; 


LEXICON CARDINALS_ONE_NINE
bezhig+Num:bezhig               # "one";
niizh+Num:niizh                 # "two";
nswi+Num:niswi                  CLOSED_PROTECTION "three";
niiwin+Num:niiwin               # "four";
naanan+Num:naanan               # "five";
!naanin:naanin               # "five";
ngodwaaswi+Num:ningodwaaswi     CLOSED_PROTECTION "six";
niizhwaaswi+Num:niizhwaaswi     CLOSED_PROTECTION "seven";
nshwaaswi+Num:nishwaaswi        CLOSED_PROTECTION "eight";
zhaangswi+Num:zhaangaswi        CLOSED_PROTECTION "nine";

LEXICON NUMERALS_META_GATEWAY
        NUMERAL_ROOTS_GATEWAY ; 
        DISTRIBUTIVE_REDUPLICATION ; 

LEXICON NUMERAL_ROOTS_GATEWAY
        !need to allow person prefixes just in case you end up with a VAI suffix, but not in any other cases
        NUMERAL_ROOT_FORMS_ONE ; 
        NUMERAL_ROOT_FORMS_EXCEPTIONAL_ONE ; 
        NUMERAL_ROOT_FORMS_TWO_NINE ; 
        NUMERAL_ROOT_FORMS_TEN ; 
@D.Person@        NUMERAL_STANDALONE_FORMS_TEN ; !you should not ever have person prefixes on standalone numerals
@D.Person@        NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN ;
@R.NUMERAL.HUNDRED@@D.NUMERAL.TEN@  NUMERAL_HALF ; 

LEXICON ORDINALS_SET
@P.NUMERAL.ORD@ NUMERAL_ROOT_FORMS_TWO_NINE ;  !used to ensure ordinals end in repetitions. currently repetitions are limited to simple roots, can't build a more complex numeral (and so it is also impossible to build a more complex ordinal)
@P.NUMERAL.ORD@ NUMERAL_ROOT_FORMS_TEN ; 


LEXICON NUMERAL_HALF
aabta+Num:aabita     CLOSED_PROTECTION "half"; 

LEXICON NUMERAL_ROOT_FORMS_EXCEPTIONAL_ONE
bezhig+Num:bezhigw6 NUMERAL_ROOT_FORMS_VERBISH ; 

LEXICON NUMERAL_ROOT_FORMS_ONE
ngod+Num:ningod2           NUMERAL_STEM_HUNDRED_GET "one"; !can't combine with mdana (10), can combine with waak (100). ordinal form is ntam (with just e-, not eko-)

LEXICON NUMERAL_ROOT_FORMS_TEN
mdaas+Num:midaa          NUMERAL_ROOT_FORMS_TEN_GET "ten"; !mdaas everywhere except repetition forms (and thus ordinals as well, then mdaas/mdaach. can't combine with mdana, can combine with waak

LEXICON NUMERAL_ROOT_FORMS_TEN_GET
@D.NUMERAL.ORD@ NUMERAL_ROOT_FORMS_TEN_S ; 
                NUMERAL_ROOT_FORMS_TEN_S_CH ; 

LEXICON NUMERAL_ROOT_FORMS_TEN_S
0:s         NUMERAL_STEM_HUNDRED_GET ;

LEXICON NUMERAL_ROOT_FORMS_TEN_S_CH
0:s         NUMERAL_ROOT_FORMS_VERBISH ; 
0:ch        NUMERAL_ROOT_FORMS_VERBISH ; 

LEXICON NUMERAL_ROOT_FORMS_TWO_NINE
niizh+Num:niizh             NUMERAL_ROOT_FORMS_GET_TWO "two";        !20 is niizhdana (no m), otherwise regular
nis+Num:nis                 NUMERAL_ROOT_FORMS_35_GATEWAY "three";   !fully regular
nii+Num:nii                 NUMERAL_ROOT_FORMS_FOUR_GATEWAY "four";  !nii everywhere, except in repetition form, then niiwing
!naan+:naanan           NUMERAL_ROOT_FORMS_35_GATEWAY "five";    !have not seen this variant, but it was mercurially listed in valentine 2001:874
naan+Num:naan               NUMERAL_ROOT_FORMS_35_GATEWAY "five";    !fully regular
ngodwaas+Num:ningodwaa     NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY "six"; !ngodwaas everywhere but repetition forms (and thus ordinals as well), then ngodwaach
niizhwaas+Num:niizhwaa     NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY "seven"; !niizhwaas everywhere but repetition forms (and thus ordinals as well), then niizhwaach
nshwaas+Num:nishwaa        NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY "eight"; !nshwaas everywhere but repetition forms (and thus ordinals as well), then nshwaach
zhaangs+Num:zhaanga        NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY "nine"; !zhaangs everywhere but repetition forms (and thus ordinals as well), then zhaangch

LEXICON NUMERAL_ROOT_FORMS_35_GATEWAY
                    NUMERAL_ROOT_FORMS_VERBISH ; 
                    NUMERAL_ROOT_FORMS_GET ; 

LEXICON NUMERAL_ROOT_FORMS_FOUR_GATEWAY
0:w                 NUMERAL_ROOT_FORMS_VERBISH ; 
                    NUMERAL_ROOT_FORMS_GET ; 

LEXICON NUMERAL_ROOT_FORMS_SIX_NINE_GATEWAY
0:s                 NUMERAL_ROOT_FORMS_GET  ; 
0:ch                NUMERAL_ROOT_FORMS_VERBISH ; 

LEXICON NUMERAL_ROOT_FORMS_VERBISH
+VII:in VII_ORDERS_GET ; !ordinals, repetition forms take +Cnj+0:g, presumably full on verbs of number can take the full suite. person prefixes will not be legitimate here because VII_ORDERS_GET rules them out
+VAI:i  VAI_ORDERS_GET ; !first and second person prefixes need to be routed here too

LEXICON NUMERAL_ROOT_FORMS_GET_TWO
                                        NUMERAL_CLASSIFICATORY_MEDIALS ; 
                                        NUMERAL_ROOT_FORMS_VERBISH ; 
@D.NUMERAL.ORD@@D.NUMERAL.TEN@         NUMERAL_ROOT_FORMS_TWO_TENS ; 
@D.NUMERAL.ORD@@D.NUMERAL.HUNDRED@     NUMERAL_STEM_HUNDRED_SET ; 

LEXICON NUMERAL_ROOT_FORMS_GET
@D.NUMERAL.ORD@@D.NUMERAL.TEN@         NUMERAL_STEM_TEN_SET ; 
@D.NUMERAL.ORD@@D.NUMERAL.HUNDRED@     NUMERAL_STEM_HUNDRED_SET ;
                                        NUMERAL_CLASSIFICATORY_MEDIALS ; 
                                        NUMERAL_FINALS ; 

LEXICON NUMERAL_STEM_TEN_SET  
@P.NUMERAL.TEN@     NUMERAL_STEM_TEN ;

LEXICON NUMERAL_ROOT_FORMS_TWO_TENS
+mdana+Num:%>dana     NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ; !need to trigger epenthesis when it is CC

LEXICON NUMERAL_STEM_TEN
+mdana+Num:%>midana     NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ; !need to trigger epenthesis when it is CC

LEXICON NUMERAL_STEM_HUNDRED_GET
@D.NUMERAL.HUNDRED@ NUMERAL_STEM_HUNDRED_SET ;

LEXICON NUMERAL_STEM_HUNDRED_SET
@P.NUMERAL.HUNDRED@ NUMERAL_STEM_HUNDRED ;

LEXICON NUMERAL_STEM_HUNDRED
+waak+Num:waak     NUMERAL_STANDALONE_FORMS_HUNDRED_GATEWAY ; 

LEXICON NUMERAL_STANDALONE_FORMS_HUNDRED_GATEWAY 
    # ; 
+shi+Conj+:-ashi-    NUMERAL_ROOTS_GATEWAY ; 

LEXICON DISTRIBUTIVE_REDUPLICATION
Redup+:CRED1e     DISTRIBUTIVE_REDUPLICATION_GATEWAY ; 
                    DISTRIBUTIVE_REDUPLICATION_GATEWAY ; 

LEXICON DISTRIBUTIVE_REDUPLICATION_GATEWAY
!this can also occur using roots in verbs (nen'godooshkne, nen'godwaasooshkne) (nenaannooshkne is not a root, would be naan)
        NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN ;
        NUMERAL_STANDALONE_FORMS_TEN ;

LEXICON NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN
bezhig+Num:bezhig               # "one";
niizh+Num:niizh                 # "two";
nswi+Num:niswi                  CLOSED_PROTECTION "three";
niiwin+Num:niiwin               # "four";
naanan+Num:naanan               # "five";
!naanin:naanin               # "five";
ngodwaaswi+Num:ningodwaaswi     CLOSED_PROTECTION "six";
niizhwaaswi+Num:niizhwaaswi     CLOSED_PROTECTION "seven";
nshwaaswi+Num:nishwaaswi        CLOSED_PROTECTION "eight";
zhaangswi+Num:zhaangaswi        CLOSED_PROTECTION "nine";

LEXICON NUMERAL_STANDALONE_FORMS_TEN !THIS NEEDS TO BE CONNECTED UPSTREAM
mdaaswi+Num:midaaswi             NUMERAL_STANDALONE_FORMS_TEN_GATEWAY ;

LEXICON NUMERAL_STANDALONE_FORMS_TEN_GATEWAY 
    CLOSED_PROTECTION ; 
+shi+Conj+:-ashi-    NUMERAL_STANDALONE_FORMS_LESS_THAN_TEN ; !form numbers X1-X9

LEXICON NUMERAL_CLASSIFICATORY_MEDIALS
!you can follow these with VAI -izi, -VII -od (ad?)
+aatig+Num:waatig    # "stick";
+eg+Num:weg    # "cloth";
+aabik+Num:waabik    # "metal/glass";
+aabiig+Num:waabiig    # "string/row";
+aak+Num:waak    # "pole";
+ooshkin+Num:ooshkin    # "bag";
+bboon+Num:%>biboon    # "winter";
+bii'gan+Num:%>biihigan    # "card";
+dbahaabaan+Num:%>d2ibahaabaan    # "mile"; !ngod loses its d from the looks of it before d here
+dbik+Num:%>d2ibik    # "night"; !ngod loses its d from the looks of it before d here
+gamig+Num:%>gamig    # "building";
+gon+Num:%>gon    # "day";
+kiins+Num:akiins    # "acre";
+naagaans+Num:%>naagaans    # "cup";
+nik+Num:%>nik    # "arm";
+ninj+Num:%>ninj    # "hand";
+waakwagan+Num:%>waakBv1wagan    # "hand";
+oode+Num:oode    # "family";
+oonag+Num:oonag    # "boat";
+sag+Num:%>sag    # "barrel";
+wewaan+Num:wewaan    # "group";
+zid+Num:%>zid    # "foot";

LEXICON NUMERAL_FINALS
!nen'godooshkne ->neningod-ooshkin-e be a max of one to each... a distributive reduplicated form (NOT bebezhig!) plus -ooshkine. there are similar ones
+VII:oonagad2 VII_ORDERS_GET "be a boat load"; !Lumping classifier material with final. oonag classifier exists, but I'm a bit leery of -ad2 as a final on its own.
+VAI:oonagizi VAI_ORDERS_GET "be a boat load"; 
+VII:%>d2oopwinigad2   VII_ORDERS_GET "be a tableful" ; !doopwin is clear, but then you have -igad2 in addition to -ad2 ... need to delete dd>d with ngod
+VAI:%>d2oopwinigizi   VAI_ORDERS_GET "be a tableful" ; !doopwin is clear, but then you have -igad2 in addition to -ad2
+VII:ooshkine       VII_ORDERS_GET "be a containerful" ; 
+VAI:ooshkine       VAI_ORDERS_GET "be a containerful" ; 
+VII:%>gonagad2      VII_ORDERS_GET "be later" ; !gon 'day' + gad? 'absent'
+VAI:%>gonendi       VAI_ORDERS_GET "be absent" ; !gon 'day' + endi 'absent'
!niizhoobhidwag niizh=2, bahidi=run oo=??
!niizhooshin niizh=2, shin=lie oo=??
+VII:waabagonii    VII_ORDERS_GET "grow" ; 
+VAI:wewaanigizi    VAI_ORDERS_GET "be groups" ; 
