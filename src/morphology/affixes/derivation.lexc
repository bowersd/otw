!#####
!#VTA Based Derivation
!#####
LEXICON VALENCE_INCREASING
+Caus:%>%>shkaw        VTA_TAG ; !"weight: 1"; !causative in body, i epenthetic?

LEXICON VTA_DETRANSITIVE_GET
                VTA_DETRANSITIVE ; 
                VTA_PREORDER_GET ;

LEXICON VTA_DETRANSITIVE
!(anti)-passives? we have indep order passives, but what about cnj?
!-(i)ge (take -am off of VTI, palatalize t/d, assimilate VTA m to n)
!, -(i)we (apply to h/N/S/n final VTAs, lots of palatalization)
!, -aaso (remove VTI theme)
!p 413 for productive VAI secondary finals
+Rcpl:%>%>idi       VAI_TAG ; !"weight: 1"; !conditions awi->aa
+Rflx:%>%>idizo     VAI_TAG ; !"weight: 1"; !conditions awi->aa
+AntPass:%>%>ge     VAI_TAG ; !"weight: 1";  !conditions awi->aa but rules will not put an [i] in the right spot for that (they put i2 before the boundary to get the palatalization)
                VTI_BASE_DERIV_VAI ; 

LEXICON VAI_DERIV_GATEWAY
        VAI_ORDERS_GET ;
        NOMINAL_COMPOUNDING_SET ;
        VAI_BASE_DERIV_NI ;
        DERIV_VII ; 
        VALENCE_INCREASING ; 

LEXICON VII_DERIV_GATEWAY  
        VII_ORDERS_GET ;
        NOMINAL_COMPOUNDING_SET ;
        DERIV_VII ; 

LEXICON VTI_BASE_DERIV
        VTI_BASE_DERIV_VAI ;
        VTI_BASE_DERIV_VII ;

LEXICON VTI_BASE_DERIV_VAI !also fed by VTA_DETRANSITIVE, since many VTAs were showing up with -gaazo
+Pass:%>%>gaazo  VAI_TAG ; !"weight: 1"; 

LEXICON VTI_BASE_DERIV_VII 
+Pass:%>%>gaade  VII_TAG ; !"weight: 1";

LEXICON DERIV_VII
0:%>%>magad2 VII_TAG ; !"weight: 1"; !what is base for magad?->VAI/VII need to enforce preceding V lengthening

LEXICON VAI_BASE_DERIV_NI
0:win NI_SET ; !"weight: 1";
0:gan NI_SET ; !"weight: 1";

LEXICON VAI_N_BASE_DERIV_NI
0:wi        VAI_N_BASE_DERIV_NI_GAMIG ; !"weight: 1"; !this is extensional w and triggers lengthening, w2 dropping (these attributes are not implemented 11/29/2023)
            VAI_N_BASE_DERIV_NI_GAMIG ; 

LEXICON VAI_N_BASE_DERIV_NI_GAMIG
0:gamigo    NI_SET ; !"weight: 1";

LEXICON N_BASE_DERIV_VAI
@R.Person.3@@C.Person@    N_BASE_DERIV_VAI_POSSESSION ; !this only allows third person possession verbs to exist. ... you need to be able to pick up a prefix for the verb, then pick up 3 on the noun, then drop 3, but keep the person information of the verbal prefixes
+Caus:%>%>ke    VAI_TAG ; !make/do
                                                !so use a verbperson diacritic and a nounperson diacritic, plus disassociate noun and verb prefixation
LEXICON N_BASE_DERIV_VAI_POSSESSION 
+Poss:i  VAI_TAG ; !"weight: 1"; !'have' (listed in V 2001 index) per p 416 in V 2001: w+i -> o. locative augment -aa appears, suppletive possessives are used (we don't have any of those!), use possessed noun instead of nominal medial, posthm is optional if not required by noun root (we don't know which ones require it, subject to dialect variation?)

LEXICON NA_BASE_DERIV_VAI
0:wi VAI_TAG ; !"weight: 1"; !'be' what flavor of w is this (phonologically)? 

LEXICON N_BASE_DERIV_VII
0:wan VII_TAG ; !"weight: 1"; !'be' what flavor of w is this (phonologically)? 
