
define hw [h (->) w || \[c|s|n|z] _ ] ; 
define hApos [h -> %' || \[c|s|n|z] _ ] ;
define aposEpen [[..] -> %' || n _ k ] ; 
define hyphEpen [[..] (->) [%-::1] ||  _ [s|z] i i ] ; 
define nasalPlaceAssim [ m (->) [n::1] || _ [d|t] ,,
                        m (->) [n %'::1] || _ [s|z|g|k] ,,
                        n (->) [m::1] || _ [b|p] ] ; 
define aposHyphDel [[%'|%-] (->) [0::1]] ; !this is allowing PV/e+ to be written with no trailing hyphen... not good
define finDevoi [b -> p || \[m] _ .#. ,,
                b (->) p || m _ .#. ,,
                d -> t, g -> k, z -> s, z h -> s h, j -> c h || \[n] _ .#. ,,
                d (->) t, g (->) k, z (->) s, z h (->) s h, j (->) c h || n _ .#.  ] ; !RV reports no devoicing after nasals, but it has been observed, so it is added as an option
define oa  [o (->) a || [g|k|s (h)|z (h)|p|b|m|n|w|%']  _ (  !optionality added to accomodate areas where o's observed in corbiere final sylls
                                                            p|t|k|c h|
                                                            [[s (h)](p|t|k)]|
                                                            [[m (b)]|[n (d|g|j|z (h)|s|h)]]|
                                                            w|y|%'
                                                            )
                                                             .#.] .o.
            [o -> a || [d|t|c h|j|y]  _ (  
                                                            p|t|k|c h|
                                                            [[s (h)](p|t|k)]|
                                                            [[m (b)]|[n (d|g|j|z (h)|s|h)]]|
                                                            w|y|%'
                                                            )
                                                             .#.
                                                             ] ; 

define nh2n [h (->) [0::1] || n _ ] ; 
define initGlideDel [y (->) [0::1] || .#.  _ [i|e],,
                    w (->) [0::1] || .#. _ o] ; 
define yayi [a (->) [i::1] || y _ \[a]] ; 

define wy [w (->) [y::1] || [a|e|i] _ ,,
          y (->) [w::1] || o _ ] ; 

define lengthGlide [[..] (->) [o::1] || \[o] o _ w ,,
                    [..] (->) [i::1] || \[i] i _ y ] ; !some ambiguity before nasals too. consider expanding?

define gnMetath [ g n (->) [n g::1]] ; 

define stridDevoi [z h (->) [s h::1], z (->) [s::1] || _ [p|t|k|b|d|g] ] ; !other devoicing possible, may expand rule, but these set up apparent primary clusters, so they got the first look !!j (->) c h also a strident, but does not make a cluster
define postStridDevoi [b (->) [p::1], d (->) [t::1], g (->) [k::1] || [s|s h] _  ] ; !This rounds out the false strident cluster generation. I don't think lenes fortite if the first member of the cluster stays lenis

define clustDevoi [b (->) [p::1], d (->) [t::1], z (->) [s::1], j (->) [c h::1], z h (->) [s h::1] || _ [p|t|k|s|c] ] ;

define homorgObsMerge [
                    b b (->) [p::1], b p (->) [p::1], p b (->) [p::1], p p (->) [p::1],
                    d d (->) [t::1], d t (->) [t::1], t d (->) [t::1], t t (->) [t::1],
                    g g (->) [k::1], g k (->) [k::1], k g (->) [k::1], k k (->) [k::1],
                    z z (->) [s::1], z s (->) [s::1], s z (->) [s::1], s s (->) [s::1],
                    z h z h (->) [s h::1], z h s h (->) [s h::1], s h z h (->) [s::1], sh sh (->) [s h::1],
                    j j (->) [c h::1], j c h (->) [c h::1], c h j (->) [c h::1], c h c h (->) [c h::1]
                    ]; 

define lenisNasAssim [ b (->) [m::1], g (->) [n g::1], d (->) [n::1] ||  _ [m|n] ] ; !from Rhodes 1985b p 468
define lenisStopDeletion [[d|b|g] (->) [0::1] || _ \[a|i|o|w|y]] ; 
define lenisFortition [ b (->) [p::1], g (->) [k::1], d (->) [t::1], j (->) [c h::1], z h (->) [s h::1], z (->) [s::1] ||  _ [p|t|k|s|c h] ] ; !from Rhodes 1985b p 468
define initialHomorgMerge    [ b b (->) [b::1], g g (->) [g::1], d d (->) [d::1] || [.#.|%-] _ ] ; !from Rhodes 1985b p 469
define initialHomorgSonMerge [ m m (->) [m::1], n n (->) [n::1], w w (->) [w::1] || [.#.|%-] _ ] ; !from Rhodes 1985b p 468

regex [ hw .o. !surfacey phonology that should not be mixed in to the practice of marking n'k clusters
        hApos .o. !character set differences
        aposEpen .o. 
        nasalPlaceAssim .o.
        aposHyphDel .o.
        finDevoi .o. !surfacey phonology
        oa .o.
        nh2n .o.
        !initGlideDel .o.
        yayi .o. !this is fed by oa, not sure if that is right
        wy .o.
        lengthGlide .o.
        !gnMetath .o.
        stridDevoi .o.
        postStridDevoi .o.
        clustDevoi .o.
        lenisNasAssim .o.
        !lenisStopDeletion .o.
        lenisFortition .o.
        !homorgObsMerge .o.
        !initialHomorgMerge .o.
        !initialHomorgSonMerge .o.
        hyphEpen
        ] ;

save stack fiero2corbiere.hfst
